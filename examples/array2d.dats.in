(*

Copyright Â© 2021 Barry Schwartz

This program is free software: you can redistribute it and/or
modify it under the terms of the GNU General Public License, as
published by the Free Software Foundation, either version 3 of the
License, or (at your option) any later version.

This program is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
General Public License for more details.

You should have received copies of the GNU General Public License
along with this program. If not, see
<https://www.gnu.org/licenses/>.

*)

#define ATS_DYNLOADFLAG 0

#include "share/atspre_define.hats"
#include "share/atspre_staload.hats"

staload "proofs.sats"
staload "array2d.sats"

implement {t}
arr2get {m, n} {na} {i, j} (m, a, i, j) =
  let
    prval _ = lemma_g1uint_param (i) (* Establish that 0 <= i. *)
    prval _ = lemma_g1uint_param (j) (* Establish that 0 <= j. *)
    prval _ = mul_gte_gte_gte {j, m} ()
    prval _ = mul_compare_lte {j, n - 1, m} ()
    prval _ = prop_verify {0 <= i + (j * m) && i + (j * m) < na} ()
  in
    a[i + (j * m)]
  end

implement {t}
arr2put {m, n} {na} {i, j} (m, a, i, j, x) =
  let
    prval _ = lemma_g1uint_param (i) (* Establish that 0 <= i. *)
    prval _ = lemma_g1uint_param (j) (* Establish that 0 <= j. *)
    prval _ = mul_gte_gte_gte {j, m} ()
    prval _ = mul_compare_lte {j, n - 1, m} ()
    prval _ = prop_verify {0 <= i + (j * m) && i + (j * m) < na} ()
  in
    a[i + (j * m)] := x
  end

implement {t}
arr2copy_block {m, n1, n2} {na1, na2} {q, r} {i1, j1} {i2, j2}
               (m, a1, a2, q, r, i1, j1, i2, j2) =
  let
    implement
    arr2map_block$filter<t> (t) = t
  in
    arr2map_block {m, n1, n2} (m, a1, a2, q, r, i1, j1, i2, j2)
  end

implement {t}
arr2map_block {m, n1, n2} {na1, na2} {q, r} {i1, j1} {i2, j2}
              (m, a1, a2, q, r, i1, j1, i2, j2) =
  let
    prval _ = lemma_g1uint_param (q)  (* Establish that 0 <= q. *)
    prval _ = lemma_g1uint_param (r)  (* Establish that 0 <= r. *)
    prval _ = lemma_g1uint_param (i1) (* Establish that 0 <= i1. *)
    prval _ = lemma_g1uint_param (j1) (* Establish that 0 <= j1. *)
    prval _ = lemma_g1uint_param (i2) (* Establish that 0 <= i2. *)
    prval _ = lemma_g1uint_param (j2) (* Establish that 0 <= j2. *)
    fun
    outer_loop {j_a1 : int | j1 <= j_a1; j_a1 <= j1 + r}
               .<(j1 + r) - j_a1>.
               (a1   : &array(t, na1),
                a2   : &array(t, na2),
                j_a1 : size_t j_a1) : void =
      let
        fun
        inner_loop {i_a1, j_a1, j_a2 : int
                      | i1 <= i_a1; i_a1 <= i1 + q;
                        j1 <= j_a1; j_a1 < j1 + r;
                        j2 <= j_a2; j_a2 < j2 + r}
                   .<(i1 + q) - i_a1>.
                   (a1   : &array(t, na1),
                    a2   : &array(t, na2),
                    i_a1 : size_t i_a1,
                    j_a1 : size_t j_a1,
                    j_a2 : size_t j_a2) : void =
          if i_a1 < i1 + q then
            let
              val i_a2 = (i_a1 - i1) + i2
              val x = arr2get {m, n1} (m, a1, i_a1, j_a1)
              val y = arr2map_block$filter(x)
              val _ = arr2put {m, n2} (m, a2, i_a2, j_a2, y)
            in
              inner_loop (a1, a2, succ i_a1, j_a1, j_a2)
            end
      in
        if j_a1 < j1 + r then
          let
            val j_a2 = (j_a1 - j1) + j2
            val _ = inner_loop (a1, a2, i1, j_a1, j_a2)
          in
            outer_loop (a1, a2, succ j_a1)
          end
      end
  in
    outer_loop (a1, a2, j1)
  end

implement {t}
arr2copy_lower_triangle {m, n} {na1, na2} (m, n, a1, a2) =
  let
    implement
    arr2map_lower_triangle$filter<t> (t) = t
  in
    arr2map_lower_triangle (m, n, a1, a2)
  end

implement {t}
arr2map_lower_triangle {m, n} {na1, na2} (m, n, a1, a2) =
  let
    prval _ = lemma_g1uint_param (n) (* Establish that 0 <= n. *)
    fun
    loop {j  : int | 0 <= j; j <= n} .<n - j>.
         (a1 : &array(t, na1),
          a2 : &array(t, na2),
          j  : size_t j) : void =
      if j < n then
        let
          implement
          arr2map_block$filter<t> = arr2map_lower_triangle$filter<t>
          (* FIXME: Using arr2map_block here may make us do a
                    little extra looping logic, though it should
                    cost little, and the cost might be eliminated
                    if loop-unrolling is done. *)
          val _ = arr2map_block {m, n, n}
                                (m, a1, a2, n - j, i2sz 1, j, j, j, j)
        in
          loop (a1, a2, succ j)
        end
  in
    loop (a1, a2, i2sz 0)
  end
